plugins {
    id "io.freefair.lombok" version "6.6.1"
    id "architectury-plugin" version "3.4-SNAPSHOT" apply false
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id "me.shedaniel.unified-publishing" version "0.1.+" apply false
}

static String getNextMajorMinecraftVersion(String currentVersion) {
    def version = currentVersion.substring(0, currentVersion.indexOf('.'))
    def majorSubstring = currentVersion.indexOf('.', currentVersion.indexOf('.') + 1)
    def majorVersion

    if (majorSubstring == -1) {
        majorVersion = currentVersion.substring(currentVersion.indexOf('.') + 1)
    } else {
        majorVersion = currentVersion.substring(currentVersion.indexOf('.') + 1, currentVersion.indexOf('.', currentVersion.indexOf('.') + 1))
    }

    def majorInt = Integer.valueOf(majorVersion)
    majorInt++
    majorVersion = majorInt.toString()
    return "${version}.${majorVersion}"
}

subprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.freefair.lombok"
    apply plugin: "me.shedaniel.unified-publishing"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        withSourcesJar()
        toolchain.languageVersion = JavaLanguageVersion.of(8)
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        mappings loom.officialMojangMappings()
    }
}